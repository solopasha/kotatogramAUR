pkgname=kotatogram-desktop
pkgver=1.4.9
pkgrel=6
pkgdesc='Kotatogram â€“ experimental Telegram Desktop fork'
arch=('x86_64')
url="https://kotatogram.github.io"
license=('GPL3')
_tg_owt_commit=442d5bb593c0ae314960308d78f2016ad1f80c3e
depends=('hunspell' 'ffmpeg' 'hicolor-icon-theme' 'lz4' 'minizip' 'openal' 'ttf-opensans'
         'qt6-imageformats' 'qt6-svg' 'qt6-wayland' 'qt6-5compat' 'xxhash' 'glibmm'
         'rnnoise' 'pipewire' 'libxtst' 'libxrandr' 'jemalloc' 'abseil-cpp' 'libdispatch'
         'openssl-1.1' 'protobuf')
makedepends=('cmake' 'git' 'ninja' 'python' 'range-v3' 'tl-expected' 'microsoft-gsl' 'meson'
             'extra-cmake-modules' 'wayland-protocols' 'plasma-wayland-protocols' 'gobject-introspection' 
             'glib2' 'yasm')
optdepends=('webkit2gtk: embedded browser features'
            'xdg-desktop-portal: desktop integration')
conflicts=('kotatogram-desktop-bin' 'kotatogram-desktop-dynamic-bin')
options=(!debug)
# git submodule foreach --quiet 'echo \"\$\{pkgname\}-${name##*/}::git+`git remote get-url origin`\"' | sort
source=("${pkgname}::git+https://github.com/ilya-fedin/kotatogram-desktop.git#branch=block-users-in-groups"
        "${pkgname}-cmake::git+https://github.com/kotatogram/cmake_helpers.git"
        "${pkgname}-codegen::git+https://github.com/desktop-app/codegen.git"
        "${pkgname}-dispatch::git+https://github.com/apple/swift-corelibs-libdispatch"
        "${pkgname}-expected::git+https://github.com/TartanLlama/expected"
        "${pkgname}-fcitx5-qt::git+https://github.com/fcitx/fcitx5-qt.git"
        "${pkgname}-fcitx-qt5::git+https://github.com/fcitx/fcitx-qt5.git"
        "${pkgname}-GSL::git+https://github.com/Microsoft/GSL.git"
        "${pkgname}-hime::git+https://github.com/hime-ime/hime.git"
        "${pkgname}-hunspell::git+https://github.com/hunspell/hunspell"
        "${pkgname}-jemalloc::git+https://github.com/jemalloc/jemalloc"
        "${pkgname}-lib_base::git+https://github.com/desktop-app/lib_base.git"
        "${pkgname}-lib_crl::git+https://github.com/desktop-app/lib_crl.git"
        "${pkgname}-lib_lottie::git+https://github.com/desktop-app/lib_lottie.git"
        "${pkgname}-lib_qr::git+https://github.com/desktop-app/lib_qr.git"
        "${pkgname}-lib_rpl::git+https://github.com/desktop-app/lib_rpl.git"
        "${pkgname}-lib_spellcheck::git+https://github.com/desktop-app/lib_spellcheck"
        "${pkgname}-lib_storage::git+https://github.com/desktop-app/lib_storage.git"
        "${pkgname}-libtgvoip::git+https://github.com/telegramdesktop/libtgvoip"
        "${pkgname}-lib_tl::git+https://github.com/desktop-app/lib_tl.git"
        "${pkgname}-lib_ui::git+https://github.com/kotatogram/lib_ui.git"
        "${pkgname}-lib_waylandshells::git+https://github.com/desktop-app/lib_waylandshells.git"
        "${pkgname}-lib_webrtc::git+https://github.com/desktop-app/lib_webrtc.git"
        "${pkgname}-lib_webview::git+https://github.com/desktop-app/lib_webview.git"
        "${pkgname}-lz4::git+https://github.com/lz4/lz4.git"
        "${pkgname}-nimf::git+https://github.com/hamonikr/nimf.git"
        "${pkgname}-plasma-wayland-protocols::git+https://github.com/KDE/plasma-wayland-protocols.git"
        "${pkgname}-QR::git+https://github.com/nayuki/QR-Code-generator"
        "${pkgname}-range-v3::git+https://github.com/ericniebler/range-v3.git"
        "${pkgname}-rlottie::git+https://github.com/desktop-app/rlottie.git"
        "${pkgname}-tgcalls::git+https://github.com/TelegramMessenger/tgcalls.git"
        "${pkgname}-wayland-protocols::git+https://github.com/gitlab-freedesktop-mirrors/wayland-protocols.git"
        "${pkgname}-xxHash::git+https://github.com/Cyan4973/xxHash.git"
        "${pkgname}-tg_owt::git+https://github.com/desktop-app/tg_owt.git#commit=${_tg_owt_commit}"
        "${pkgname}-libyuv::git+https://gitlab.com/chromiumsrc/libyuv.git"
        "${pkgname}-src::git+https://github.com/google/crc32c.git"
        "https://github.com/TelegramMessenger/tgcalls/commit/82c4921045c440b727c38e464f3a0539708423ff.patch"
        "block-sponsored_messages.patch"
        "25012.patch"
        "tg_owt2.patch"
)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '43944d9d50c474b82dd4cf025ce219e9d1573bbb5ba34ec6b44e77cd9154047af007cd5f3d66afa97f4c533c7f97aadb92db0dee860724abfd44f05b3c7835cb'
            '642d509804ba97af131de0927f4a7c4ce28c847564e51d50dd29c81976ad6aa12c11b8bca783488e85d737ba6850400461550320e83fe54a242b35513b146d45'
            '9d79f9d877930692d7c6e361b7b5c0b231f73e5b5d38c85c7a35965fdb8f1062b7470e98b1284643e56a0208d79e8aa4191834b94dc8a840e80d17b7b262425a'
            '6a8392d064e28f09248c3155d18e862dfdb43a4a8fedf45ec1e495c9dd35efec6bef0a990ae001e49c927a3e44f5493624833475fe22ad7238deaebd7c8de89e')

# git submodule foreach --quiet 'echo git config submodule.$name.url \"\$srcdir/$\{pkgname\}-${name##*/}\"' | sort
prepare() {
    cd "${srcdir}/${pkgname}"
    git submodule init
    git config submodule.cmake.url "$srcdir/${pkgname}-cmake"
    git config submodule.Telegram/codegen.url "$srcdir/${pkgname}-codegen"
    git config submodule.Telegram/lib_base.url "$srcdir/${pkgname}-lib_base"
    git config submodule.Telegram/lib_crl.url "$srcdir/${pkgname}-lib_crl"
    git config submodule.Telegram/lib_lottie.url "$srcdir/${pkgname}-lib_lottie"
    git config submodule.Telegram/lib_qr.url "$srcdir/${pkgname}-lib_qr"
    git config submodule.Telegram/lib_rpl.url "$srcdir/${pkgname}-lib_rpl"
    git config submodule.Telegram/lib_spellcheck.url "$srcdir/${pkgname}-lib_spellcheck"
    git config submodule.Telegram/lib_storage.url "$srcdir/${pkgname}-lib_storage"
    git config submodule.Telegram/lib_tl.url "$srcdir/${pkgname}-lib_tl"
    git config submodule.Telegram/lib_ui.url "$srcdir/${pkgname}-lib_ui"
    git config submodule.Telegram/lib_waylandshells.url "$srcdir/${pkgname}-lib_waylandshells"
    git config submodule.Telegram/lib_webrtc.url "$srcdir/${pkgname}-lib_webrtc"
    git config submodule.Telegram/lib_webview.url "$srcdir/${pkgname}-lib_webview"
    git config submodule.Telegram/ThirdParty/dispatch.url "$srcdir/${pkgname}-dispatch"
    git config submodule.Telegram/ThirdParty/expected.url "$srcdir/${pkgname}-expected"
    git config submodule.Telegram/ThirdParty/fcitx5-qt.url "$srcdir/${pkgname}-fcitx5-qt"
    git config submodule.Telegram/ThirdParty/fcitx-qt5.url "$srcdir/${pkgname}-fcitx-qt5"
    git config submodule.Telegram/ThirdParty/GSL.url "$srcdir/${pkgname}-GSL"
    git config submodule.Telegram/ThirdParty/hime.url "$srcdir/${pkgname}-hime"
    git config submodule.Telegram/ThirdParty/hunspell.url "$srcdir/${pkgname}-hunspell"
    git config submodule.Telegram/ThirdParty/jemalloc.url "$srcdir/${pkgname}-jemalloc"
    git config submodule.Telegram/ThirdParty/libtgvoip.url "$srcdir/${pkgname}-libtgvoip"
    git config submodule.Telegram/ThirdParty/lz4.url "$srcdir/${pkgname}-lz4"
    git config submodule.Telegram/ThirdParty/nimf.url "$srcdir/${pkgname}-nimf"
    git config submodule.Telegram/ThirdParty/plasma-wayland-protocols.url "$srcdir/${pkgname}-plasma-wayland-protocols"
    git config submodule.Telegram/ThirdParty/QR.url "$srcdir/${pkgname}-QR"
    git config submodule.Telegram/ThirdParty/range-v3.url "$srcdir/${pkgname}-range-v3"
    git config submodule.Telegram/ThirdParty/rlottie.url "$srcdir/${pkgname}-rlottie"
    git config submodule.Telegram/ThirdParty/tgcalls.url "$srcdir/${pkgname}-tgcalls"
    git config submodule.Telegram/ThirdParty/wayland-protocols.url "$srcdir/${pkgname}-wayland-protocols"
    git config submodule.Telegram/ThirdParty/xxHash.url "$srcdir/${pkgname}-xxHash"
    git -c protocol.file.allow=always submodule update
    patch -Np1 -i "${srcdir}/25012.patch"
    patch -Np1 -i "${srcdir}/block-sponsored_messages.patch"
    patch -Np1 -d Telegram/ThirdParty/tgcalls -i "${srcdir}/82c4921045c440b727c38e464f3a0539708423ff.patch"
    cd "${srcdir}/${pkgname}-tg_owt"
    git submodule init
    git config submodule.src/third_party/crc32c/src.url "$srcdir/${pkgname}-src"
    git config submodule.src/third_party/libyuv.url "$srcdir/${pkgname}-libyuv"
    git -c protocol.file.allow=always submodule update
    patch -Np1 -i "${srcdir}/tg_owt2.patch"
}

build() {
    export PKG_CONFIG_PATH='/usr/lib/openssl-1.1/pkgconfig'
    export CFLAGS+=" -ffat-lto-objects" 
    export CXXFLAGS+=" -ffat-lto-objects"
    cd "${srcdir}/${pkgname}-tg_owt"
    cmake \
        -B build \
        -G Ninja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_POSITION_INDEPENDENT_CODE=ON \
        -DBUILD_SHARED_LIBS=OFF \
        -DTG_OWT_USE_PROTOBUF=OFF \
        -DTG_OWT_PACKAGED_BUILD=ON
    cmake --build build

    cd "${srcdir}/${pkgname}"
    cmake \
        -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -Dtg_owt_DIR="${srcdir}/${pkgname}-tg_owt/build" \
        -DDESKTOP_APP_QT6=OFF \
        -DTDESKTOP_API_TEST=ON
    cmake --build build
}

package() {
    cd "${srcdir}/${pkgname}"
    DESTDIR="$pkgdir" cmake --install build
}
